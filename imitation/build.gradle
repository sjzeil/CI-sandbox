plugins {
   id 'java'
   id "org.xbib.gradle.plugin.git" version "2.0.0"
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.0'
    testImplementation 'org.hamcrest:hamcrest-library:2.2'
}

test {
    ignoreFailures = true
    useJUnitPlatform()
}

task reports (dependsOn: ['javadoc', 'test']) {}

////////  Website publication on GitHub pages ///////////////////


task clonePages() { 
    doLast {
        mkdir 'build/gh-pages'
        def thisRepo = rootProject.projectDir.toString() + '/..'
        def pagesDir = "$buildDir/gh-pages"
        project.delete {
            delete pagesDir
        }
        def grgit = git.clone {
            dir = pagesDir
            uri = 'file:' + thisRepo
            bare = false
            refToCheckout = 'gh-pages'
        }
        grgit.checkout {
            branch = 'gh-pages'
        }
        grgit.close()
    }
}

/* Alternate form without grgit
task clonePages() { 
    doLast {
        mkdir 'build/gh-pages'
        def thisRepo = rootProject.projectDir.toString() + '/..'
        def pagesDir = "$buildDir/gh-pages"
        project.delete {
            delete pagesDir
        }
        exec {
            workingDir = 'build/gh-pages'
            commandLine = ['git', 'clone', 'file:' + thisRepo]
        }
        exec {
            workingDir = 'build/gh-pages'
            commandLine = ['git', 'checkout', 'gh-pages']
        }
    }
}
*/


task copyWebsite (dependsOn: ['reports', 'clonePages']) {
    doLast {
        ant.copy (todir: 'build/gh-pages') {
            fileset(dir: 'build/reports')
        }
        ant.copy (todir: 'build/gh-pages') {
            fileset(dir: 'build/docs')
        }
        ant.copy (todir: 'build/gh-pages') {
            fileset(dir: 'src/main/html')
        }
    }
}



task updateGHPages (dependsOn: ['copyWebsite']) {
    group = "Reporting"
    description  'Copies reports to the gh-pages branch in preparation for a future push to GitHub'
    doLast {
        def pagesDir = "$buildDir/gh-pages"
        def grgit = git.open {
            dir = pagesDir
        }
        grgit.add (update: false, patterns: ['javadoc', 'tests', 'index.md'])
        grgit.add (update: true, patterns: ['javadoc', 'tests', 'index.md'])
        grgit.commit {
            message = "Updating web pages"
        }
        grgit.push {}
        grgit.close()
    }
}

/* Alternate form without grgit
task updateGHPages (dependsOn: 'copyWebsite') {
    group = "Reporting"
    description  'Copies reports to the gh-pages branch in preparation for a future push to GitHub'
    doLast {
        def pagesDir = "$buildDir/gh-pages"
        exec {
            workingDir = 'build/gh-pages'
            commandLine = ['git', 'add', '.']
        }
        exec {
            workingDir = 'build/gh-pages'
            commandLine = ['git', 'commit', '-m', 'Updating-webpages']
        }
        exec {
            workingDir = 'build/gh-pages'
            commandLine = ['git', 'push']
        }
    }
}
*/





task deployReports (dependsOn: 'updateGHPages') {
    doLast {
        exec {
            workingDir ='.'
            commandLine = ['git', 'push', 'origin', 'gh-pages']
        }
    }
}
