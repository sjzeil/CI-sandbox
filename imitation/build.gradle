plugins {
   id 'java'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.0'
    testImplementation 'org.hamcrest:hamcrest-library:2.2'
}

test {
    ignoreFailures = true
    useJUnitPlatform()
}

task timeStamp {
    outputs.file  "build/reports/timeStamp.md"
    doLast {
        Date now = new Date()
        FileWriter out = new FileWriter("build/reports/timeStamp.md")
        out.write("# Generated at\n\n" + now + "\n")
        out.close()
    }
}

task reports (dependsOn: ['javadoc', 'test', 'timeStamp']) {}

////////  Website publication on GitHub pages ///////////////////

def pagesDir = file('build/gh-pages').absolutePath

task clearPages(type: Delete) {
    delete pagesDir
}

task workTree(dependsOn: ['clearPages']) { 
    doLast {
        exec {
            commandLine = ['git', 'worktree', 'add', '-f', pagesDir, 'gh-pages']
        }
        exec {
            commandLine = ['git', '-C', pagesDir, 'checkout', 'gh-pages']
        }
    }
}


task copyReports (type: Copy, dependsOn: ['reports', 'workTree']) {
    from 'build/reports'
    into 'build/gh-pages'
}

task copyDocs (type: Copy, dependsOn: ['javadoc', 'workTree']) {
    from 'build/docs'
    into 'build/gh-pages'
}

task copyWebpages (type: Copy, dependsOn: ['workTree']) {
    from 'src/main/html'
    into 'build/gh-pages'
}

task copyWebsite (type: Exec, dependsOn: ['copyReports', 'copyDocs', 'copyWebpages']) {
    commandLine = ['git', '-C', pagesDir, 'status']
}


task deployReports (dependsOn: 'copyWebsite') {
    group = "Reporting"
    description  'Commit and push website changes to GitHub'
    doLast {
        Date now = new Date()
        exec {
            commandLine = ['git', '-C', pagesDir, 'status']
            ignoreExitValue = true
        }
        exec {
            commandLine = ['git', '-C', pagesDir, 'add', pagesDir]
            ignoreExitValue = true
        }
        exec {
            commandLine = ['git', '-C', pagesDir, 'commit', '-m', 'Updating webpages at ' + now.toString()]
            ignoreExitValue = true
        }
        exec {
            commandLine = ['git', '-C', pagesDir, 'push']
            ignoreExitValue = true
        }
    }
}

