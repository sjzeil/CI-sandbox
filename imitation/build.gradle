plugins {
   id 'java'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.0'
    testImplementation 'org.hamcrest:hamcrest-library:2.2'
}

test {
    ignoreFailures = true
    useJUnitPlatform()
}

task timeStamp {
    doLast {
        Date now = new Date()
        FileWriter out = new FileWriter("build/reports/timeStamp.md")
        out.write("# Generated at\n\n" + now + "\n")
        out.close()
    }
}

task reports (dependsOn: ['javadoc', 'test', 'timeStamp']) {}

////////  Website publication on GitHub pages ///////////////////

def pagesDir = 'build/gh-pages'

task clearPages(type: Delete) {
    delete pagesDir
}

task workTree(type: Exec, dependsOn: ['clearPages']) { 
    workingDir = '.'
    commandLine = ['git', 'worktree', 'add', '-f', pagesDir, 'origin/gh-pages']
}

task wtCheckout(type: Exec, dependsOn: ['workTree']) { 
    workingDir = pagesDir
    commandLine = ['git', 'checkout', 'HEAD']
}

task copyReports (type: Copy, dependsOn: ['reports', 'wtCheckout']) {
    from 'build/reports'
    into 'build/gh-pages'
}

task copyDocs (type: Copy, dependsOn: ['javadoc', 'wtCheckout']) {
    from 'build/docs'
    into 'build/gh-pages'
}

task copyWebpages (type: Copy, dependsOn: ['wtCheckout']) {
    from 'src/main/html'
    into 'build/gh-pages'
}

task copyWebsite (type: Exec, dependsOn: ['copyReports', 'copyDocs', 'copyWebpages']) {
    workingDir = pagesDir
    commandLine = ['git', 'status']
}



task deployReports (dependsOn: 'copyWebsite') {
    group = "Reporting"
    description  'Commit and push website changes to GitHub'
    doLast {
        Date now = new Date()
        exec {
            workingDir = pagesDir
            commandLine = ['git', 'status']
            ignoreExitValue = true
        }
        exec {
            workingDir = pagesDir
            commandLine = ['git', 'add', '.']
            ignoreExitValue = true
        }
        exec {
            workingDir = pagesDir
            commandLine = ['git', 'commit', '-m', 'Updating webpages at ' + now.toString()]
            ignoreExitValue = true
        }
        exec {
            workingDir = pagesDir
            commandLine = ['git', 'push']
            ignoreExitValue = true
        }
    }
}

